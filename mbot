#!/usr/bin/env perl
use strict;
use warnings;

binmode STDOUT, ":utf8";

=pod

=head1 NAME

mbot - For fetching media from podcast feeds

=head1 SYNOPSIS

For use on Android devices via SL4A (see http://code.google.com/p/android-scripting/)

or via command line like so.

    > magbot

=head1 AUTHOR

Delon Newman <delon.newman@gmail.com>

=cut

# Settings
my @FORMATS   = qw{ MP3 EPUB };
my @LANGUAGES = qw{ E };
my @MAGS      = qw{ w wp g };

my $DIR = {
    audio => "$ENV{HOME}/Podcasts",
    pub   => ["/media/NOOK/My Files/Magazines", "$ENV{HOME}/Books"]
};

my %FORMATS = (
    MP3  => 'audio',
    M4B  => 'audio',
    EPUB => 'pub',
    PDF  => 'pub'
);
    
package Parser;

sub new {
    my $class   = shift;
    my $content = shift;

    my $obj = bless {
        content => $content,
    }, $class;

    $obj->parse;

    $obj;
}

sub content { shift->{content} }

package Feed;
use base qw(Parser);

use LWP::Simple;

# Constructors

sub new {
    my ($class, %args) = @_;
    bless \%args, $class;
}

sub parse {
    my    $class = shift;
    local $_     = shift; # content
    my    %args  = ();

    /<channel>(.*)<\/channel>/gs;
    my $channel = $1;

    /<title>(.*)<\/title>/;
    $args{title} = $1;

    /<image>\s+<url>(.*)<\/url>/s;
    $args{image} = $1;

    /<atom:link href="(.*)" rel/s;
    $args{url} = $1;

    /<description>(.*)<\/description>/;
    $args{description} = $1;

    my $pub;

    ($pub, $args{format}) = split ' ', $args{description};
    $pub =~ /([a-z]+)([A-Z]+)/;
    ($args{mag}, $args{lang}) = ($1, $2);

    my $self = $class->new(%args);

    s/\s+<title>.*<\/itunes:block>//sg;
    $self->items($_);

    $self;
}

# Attributes

sub title       { shift->{title} }
sub image       { shift->{image} }
sub description { shift->{description} }
sub url         { shift->{url} }
sub format      { shift->{format} }
sub mag         { shift->{mag} }
sub lang        { shift->{lang} }

# Methods

sub dir {
    my $self = shift;
    $self->root_dir . '/' . $self->title;
}

sub root_dir {
    my $self = shift;
    my $type = $FORMATS{$self->format};

    if ( ref $DIR eq 'HASH' ) {
        if ( ref $DIR->{$type} eq 'ARRAY' ) {
            if ( -e $DIR->{$type}->[0] ) { $DIR->{$type}->[0] }
            else                         { $DIR->{$type}->[1] }
        }
        else { $DIR->{$type} }
    }
    else { $DIR }
}

sub items {
    my ($self, $content) = @_;

    if ( $content ) {
        $self->{items} = [
            map   { Item->new($_, $self) }
            grep  { !/^\n\n\s+/ }
            map   { s/^\s+<item>\s+//; $_ }
            split /<\/item>/, $content
        ];
    } else {
        if ( $self->{items} ) {
            wantarray ? @{ $self->{items} } : $self->{items};
        } else {
            wantarray ? () : [];
        }
    }
}

package Item;
use base qw(Parser);
use File::Basename;

my %tokens = (
    title   => 'title',
    link    => 'link',
    pubDate => 'date'
);

sub new {
    my ($class, $content, $feed) = @_;
    
    my $self = $class->SUPER::new($content);

    $self->{feed} = $feed;

    bless $self, $class;
}

sub title { shift->{title} }
sub link  { shift->{link} }
sub date  { shift->{date} }
sub feed  { shift->{feed} }

sub issue_dir {
    my $self = shift;
    $self->feed->dir . '/' . $self->date;
}

sub dir {
    my $self = shift;
    $self->issue_dir . '/' . $self->file;
}

sub file  {
    my $self = shift;
    $self->{file} //= basename $self->link if $self->link;

    $self->{file};
}

sub parse {
    my $self   = shift;
    local $_   = $self->content;

    for my $token (keys %tokens) {
        /<$token>(.*)<\/$token>/s;
        $self->{$tokens{$token}} = $1;
    }
}

package main;
use File::Basename;
use YAML qw(Load);

BEGIN {
    #
    # Try loading Android (from SL4A), then create notify
    # function which sends a notification message on Android
    # or prints to STDOUT anywhere else
    #
    my $droid;
    eval {
        require Android;
        Android->import;
        $droid //= Android->new;
    };

    sub notify {
        if ( $droid ) { $droid->notify('magbot', @_) }
        else          { print @_, "\n" }
    }

    #
    # Try loading LWP::Simple, otherwise use HTTP::Tiny
    # to create work alikes for 'get' and 'getstore'
    #
    eval {
        require LWP::Simple;
        LWP::Simple->import;
    };
    if ( $@ ) {
        require HTTP::Tiny;
        HTTP::Tiny->import;
    
        no strict 'subs';
        
        *get = sub { HTTP::Tiny->new->get(@_)->{content} };
    
        *getstore = sub {
            my $url  = shift;
            my $file = shift;
        
            my $content = get $url;
            open FILE, '>', $file or die "cannot open $file";
            print FILE $file;
            close FILE;
        };
    
        use strict 'subs';
    }

    # Was an alias for File::Spec but SL4A doesn't include it :-(
    sub path {
        for ( @_ ) { return "" unless defined $_ }
        join '/', @_
    }
}

sub download_feeds {
    my @urls     = @_;
    my @contents = map { get $_ } @urls;

    wantarray ? @contents : \@contents;
}

sub find_new {
    map  {  $_->[1] }
    grep { !$_->[0] }
    map  { map { [ -e $_->dir, $_ ]  } @{$_->items} } @_;
}

sub download_media {
    my @urls = @_;

    notify "Downloading feeds and checking for new items...";

    my @items = find_new map { Feed->parse($_) } download_feeds @urls;
    if ( int(@items) == 0 ) {
        notify "There's nothing new in your feeds.";
        exit;
    }

    notify "Downloading ", int(@items), " items...\n";
    
    for my $item (@items) {
        my $root_dir = $item->feed->root_dir;
        mkdir $root_dir unless -e $root_dir;

        my $feed_dir = $item->feed->dir;
        mkdir $feed_dir unless -e $feed_dir;

        my $issue_dir = $item->issue_dir;
        my $path      = $item->dir;

        mkdir $issue_dir unless -e $issue_dir;

        if ( -e $path ) {
            print "  '", $item->title, "' exists at\n   --> '", $path, "'\n\n";
        } elsif ( !$item->link ) {
            ; # skip
        } else {
            print "  downloading '", $item->title, "' to \n  --> ", $path, "\n";

            getstore $item->link, $path unless -e $path;
            print "  ok.\n\n";
        }
    }
    
    notify "DONE.";
}

sub apply {
    my ($fn) = shift;
    map { $fn->($_) } @_;
}

# partially applied function to da FACE!!
sub gen_urls {
    my ($format) = @_;

    my %types = (
        audio => 'sFFZRQVNZNT',
        pub   => 'sFFCsVrGZNT'
    );
    
    my $type = $FORMATS{$format};

    sub {
        my ($mag) = @_;
        sub {
            my ($lang) = @_;
            "http://www.jw.org/index.xjp?option=$types{$type}&rln=$lang&rmn=$mag&rfm=$format";
        }
    }
}

# Now to the main event!!
my @urls = map { apply $_, @LANGUAGES } map { apply $_, @MAGS } map { gen_urls $_ } @FORMATS;
download_media @urls if __FILE__ eq $0;
